virtualenvwrapper_verify_workon_home || exit 1
virtualenvwrapper_verify_active_environment || exit 1

set site_packages = "`virtualenvwrapper_get_site_packages_dir`"

if ( ! -d "$site_packages" ) then
    echo "ERROR: currently-active virtualenv does not appear to have a site-packages directory" > /dev/stderr
    unset site_packages
    exit 1
endif

set path_file = "$site_packages/virtualenv_path_extensions.pth"

if ( $#argv == 0 ) then
    echo "Usage: add2virtualenv dir [dir ...]"
    if ( -f "$path_file" ) then
        echo
        echo "Existing paths:"
        cat "$path_file"
    endif
    unset site_packages
    unset path_file
    exit 1
endif

touch "$path_file"
foreach pydir ($argv)
    set absolute_path = `"$VIRTUALENVWRAPPER_PYTHON" -c "import os; print os.path.abspath('$pydir')"`
    if ( "$absolute_path" != "$pydir" ) then
        echo "Warning: Converting '$pydir' to '$absolute_path'" > /dev/stderr
    endif
    echo "$absolute_path" >> "$path_file"
end

unset pydir
unset absolute_path
unset path_file
unset site_packages

exit 0

